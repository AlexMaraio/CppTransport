# determine path to runtime system .h files
FIND_PATH(CPPTRANSPORT_RUNTIME_INCLUDE     NAMES transport.h PATHS @CMAKE_INSTALL_PREFIX@ PATH_SUFFIXES include/cpptransport/transport-runtime)
FIND_PATH(CPPTRANSPORT_SQLITE_INCLUDE      NAMES sqlite3.h)

# determine path to pre-installed SPLINTER and JsonCpp libraries
FIND_LIBRARY(CPPTRANSPORT_SPLINTER_LIBRARY NAMES splinter-3-0 PATHS @CMAKE_INSTALL_PREFIX@ PATH_SUFFIXES lib/cpptransport/lib NO_DEFAULT_PATH)
FIND_LIBRARY(CPPTRANSPORT_JSONCPP_LIBRARY  NAMES jsoncpp PATHS @CMAKE_INSTALL_PREFIX@ PATH_SUFFIXES lib64/cpptransport/lib64 NO_DEFAULT_PATH)
FIND_LIBRARY(CPPTRANSPORT_SQLITE_LIBRARY   NAMES sqlite3)

# determine paths for system installation of Boost
FIND_PACKAGE(Boost REQUIRED COMPONENTS log system filesystem random timer date_time mpi thread serialization program_options regex)

# Boost threads requires threading library
FIND_PACKAGE(Threads)

# determine paths for system installation of MPI
FIND_PACKAGE(MPI REQUIRED)


ADD_LIBRARY(splinter_library SHARED IMPORTED GLOBAL)
SET_TARGET_PROPERTIES(splinter_library PROPERTIES
                      IMPORTED_LOCATION ${CPPTRANSPORT_SPLINTER_LIBRARY}
                      LINKER_LANGUAGE CXX
                      MACOSX_RPATH TRUE
                      SKIP_BUILD_RPATH FALSE
                      BUILD_WITH_INSTALL_RPATH TRUE
                      INSTALL_RPATH_USE_LINK_PATH TRUE
                      INSTALL_NAME_DIR "@rpath/")

ADD_LIBRARY(jsoncpp_library SHARED IMPORTED GLOBAL)
SET_TARGET_PROPERTIES(jsoncpp_library PROPERTIES
                      IMPORTED_LOCATION ${CPPTRANSPORT_JSONCPP_LIBRARY}
                      LINKER_LANGUAGE CXX
                      MACOSX_RPATH TRUE
                      SKIP_BUILD_RPATH FALSE
                      BUILD_WITH_INSTALL_RPATH TRUE
                      INSTALL_RPATH_USE_LINK_PATH TRUE
                      INSTALL_NAME_DIR "@rpath/")

ADD_LIBRARY(sqlite_library SHARED IMPORTED GLOBAL)
SET_TARGET_PROPERTIES(sqlite_library PROPERTIES
                      IMPORTED_LOCATION ${CPPTRANSPORT_SQLITE_LIBRARY}
                      LINKER_LANGUAGE C)


SET(CPPTRANSPORT_INCLUDE_DIRS
  ${CPPTRANSPORT_RUNTIME_INCLUDE}/..
  ${CPPTRANSPORT_RUNTIME_INCLUDE}/../SPLINTER
  ${CPPTRANSPORT_SQLITE_INCLUDE}
  ${Boost_INCLUDE_DIRS}
  ${MPI_CXX_INCLUDE_PATH}
)


SET(CPPTRANSPORT_LIBRARIES
  splinter_library
  jsoncpp_library
  sqlite_library
  ${Boost_LIBRARIES}
  ${MPI_LIBRARIES}
  ${CMAKE_THREAD_LIBS_INIT}
)


INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(CppTransport DEFAULT_MSG
  CPPTRANSPORT_RUNTIME_INCLUDE
  CPPTRANSPORT_SQLITE_INCLUDE
  CPPTRANSPORT_SPLINTER_LIBRARY
  CPPTRANSPORT_JSONCPP_LIBRARY
  CPPTRANSPORT_SQLITE_LIBRARY
  )


MARK_AS_ADVANCED(
  CPPTRANSPORT_RUNTIME_INCLUDE
  CPPTRANSPORT_SQLITE_INCLUDE
)
